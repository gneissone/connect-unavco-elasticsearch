PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:   <http://www.w3.org/2002/07/owl#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX vitro: <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX bibo: <http://purl.org/ontology/bibo/>
PREFIX ec: <https://library.ucar.edu/earthcollab/schema#>
PREFIX vlocal: <http://connect.unavco.org/ontology/vlocal#>
PREFIX cito: <http://purl.org/spar/cito/>
PREFIX obo:	<http://purl.obolibrary.org/obo/>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>

DESCRIBE ?organization ?mostSpecificType ?label ?state ?country ?role
?unavcoMemberRep ?perrole ?superOrg ?subOrg ?person ?interval ?end
WHERE {
  ?organization vitro:mostSpecificType ?mostSpecificType .
  ?organization rdfs:label ?label .
  OPTIONAL {
      ?organization obo:RO_0001025 ?state .
      ?state a vivo:StateOrProvince . }
  OPTIONAL {
      ?organization obo:RO_0001025 ?country .
      ?country a vivo:Country . }
  OPTIONAL {
      ?organization obo:RO_0000053 ?role .
      ?role vivo:roleContributesTo <http://connect.unavco.org/individual/org253530> . }
  OPTIONAL {
      ?organization vlocal:hasLiaison ?unavcoMemberRep .
  }
  OPTIONAL { ?organization vivo:relatedBy ?perrole .
             ?perrole a vivo:Position .
             OPTIONAL { ?perrole vivo:dateTimeInterval ?interval .
                        ?interval vivo:end ?end . }
             ?perrole vivo:relates ?person .
             ?person a foaf:Person }
  OPTIONAL {
         ?organization obo:BFO_0000051 ?subOrg .
         ?subOrg a foaf:Organization .
         ?subOrg rdfs:label ?subOrgLabelzz .
  }
  OPTIONAL {
      ?organization obo:BFO_0000050 ?superOrg .
      ?superOrg a foaf:Organization .
      ?superOrg rdfs:label ?superOrgLabelzz .
  }
}
