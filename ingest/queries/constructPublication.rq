PREFIX  dc:   <http://purl.org/dc/elements/1.1/>
PREFIX  foaf: <http://xmlns.com/foaf/0.1/>
PREFIX  cito: <http://purl.org/spar/cito/>
PREFIX  vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX  ec:   <https://library.ucar.edu/earthcollab/schema#>
PREFIX  obo:  <http://purl.obolibrary.org/obo/>
PREFIX  rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX  vitro: <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX  dct:  <http://purl.org/dc/terms/>
PREFIX  bibo: <http://purl.org/ontology/bibo/>
PREFIX  xsd:  <http://www.w3.org/2001/XMLSchema#>
PREFIX  owl:  <http://www.w3.org/2002/07/owl#>
PREFIX  rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX  vivo: <http://vivoweb.org/ontology/core#>
PREFIX  vlocal: <http://connect.unavco.org/ontology/vlocal#>

CONSTRUCT
  { ?publication rdf:type ?pubType .
    ?pubType rdfs:label ?pubTypeLabel .
    ?publication vitro:mostSpecificType ?mostSpecificType .
    ?publication rdfs:label ?label .
    ?publication bibo:doi ?doi .
    ?publication bibo:abstract ?abstract .
    ?publication vivo:dateTimeValue ?publicationObj .
    ?publicationObj vivo:dateTime ?publicationYear .
    ?publication bibo:presentedAt ?event .
    ?event rdfs:label ?eventLabel .
    ?publication vivo:hasPublicationVenue ?venue .
    ?publication vlocal:timesCited ?timesCited .
    ?venue rdfs:label ?venueLabel .
    ?venue vivo:publisher ?publisher .
    ?publisher rdfs:label ?publisherLabel .
    ?publication vivo:hasSubjectArea ?subjectArea .
    ?subjectArea rdfs:label ?subjectAreaLabel .
    ?publication cito:citesAsDataSource ?citation .
    ?citation rdfs:label ?citationLabel .
    ?publication vivo:relatedBy ?authorship .
    ?authorship rdf:type vivo:Authorship .
    ?authorship vivo:relates ?author .
    ?author rdf:type ?authorType .
    ?author rdfs:label ?authorLabel .
    ?authorship rdf:type ?authorshipType .
    ?authorship vivo:rank ?rank .
    ?author vcard:hasName ?vcardName .
    ?vcardName vcard:givenName ?vcardgName .
    ?vcardName vcard:familyName ?vcardfName .}
WHERE
  { GRAPH <http://vitro.mannlib.cornell.edu/default/vitro-kb-2> {
   { ?publication rdf:type ?pubType .
      ?pubType rdfs:label ?pubTypeLabel .
        ?publication rdfs:label ?label
      }
    UNION
      { ?publication rdf:type ?pubType .
        ?publication bibo:doi ?doi
      }
    UNION
      { ?publication rdf:type ?pubType .
        ?publication bibo:abstract ?abstract
      }
    UNION
      { ?publication rdf:type ?pubType .
        ?publication vivo:dateTimeValue ?publicationObj .
        ?publicationObj vivo:dateTime ?publicationYear
      }
    UNION
      { ?publication rdf:type ?pubType .
        ?publication bibo:presentedAt ?event .
        ?event rdfs:label ?eventLabel
      }
    UNION
      { ?publication rdf:type ?pubType .
        ?publication vivo:hasPublicationVenue ?venue .
        ?venue rdfs:label ?venueLabel
      }
    UNION
      { ?publication rdf:type ?pubType .
        ?publication vlocal:timesCited ?timesCited
      }
    UNION
      { ?publication rdf:type ?pubType .
        ?publication vivo:hasPublicationVenue ?venue .
        ?venue vivo:publisher ?publisher .
        ?publisher rdfs:label ?publisherLabel
      }
    UNION
      { ?publication rdf:type ?pubType .
        ?publication vivo:hasSubjectArea ?subjectArea .
        ?subjectArea rdfs:label ?subjectAreaLabel
      }
    UNION
      { ?publication rdf:type ?pubType .
        ?publication cito:citesAsDataSource ?citation .
        ?citation rdfs:label ?citationLabel
      }
    UNION
      { ?publication rdf:type ?pubType .
        ?publication vivo:relatedBy ?authorship .
        ?authorship rdf:type vivo:Authorship .
        ?authorship rdf:type ?authorshipType .
        ?authorship vivo:relates ?author .
        ?author rdf:type ?authorType .
        ?author rdfs:label ?authorLabel
      }
    UNION
      { ?publication rdf:type ?pubType .
        ?publication vivo:relatedBy ?authorship .
        ?authorship rdf:type vivo:Authorship .
        ?authorship vivo:rank ?rank
      }
    UNION
      { ?publication rdf:type ?pubType .
        ?publication vivo:relatedBy ?authorship .
        ?authorship rdf:type vivo:Authorship .
        ?authorship vivo:relates ?author .
        ?author rdf:type vcard:Individual .
        ?author rdf:type ?authorType .
        ?author vcard:hasName ?vcardName .
        ?vcardName vcard:familyName ?vcardfName .
        ?vcardName vcard:givenName ?vcardgName
      }

  }   FILTER ( ( ( ( ( ( ( ?pubType = bibo:AcademicArticle ) || ( ?pubType = vivo:Abstract ) ) || ( ?pubType = bibo:Article ) ) || ( ?pubType = bibo:Book ) ) || ( ?pubType = bibo:Chapter ) ) || ( ?pubType = bibo:Thesis ) ) || ( ?pubType = vivo:ConferencePaper ) ) }
